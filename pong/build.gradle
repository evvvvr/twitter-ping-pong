dependencies {
    compile "org.twitter4j:twitter4j-stream:3.0.3"
}

apply plugin: "application"

mainClassName = "twitter_ping_pong.pong.Program"

startScripts {
    classpath += files('src/main/conf')

    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\conf', '%APP_HOME%\\conf')
        unixScriptFile.text    = unixScriptFile.text.replace('$APP_HOME/lib/conf', '$APP_HOME/conf')
   }
}

task processConfigs(type:Copy) {
    from "src/main/conf"
    into ("$buildDir/conf")

    def sensitiveProperties = new Properties()
    file("sensitive.properties").withInputStream{
        //noinspection GroovyAssignabilityCheck
        sensitiveProperties.load(it);
    }

    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: sensitiveProperties)
}

applicationDistribution.from(processConfigs) {
    into "conf"
}